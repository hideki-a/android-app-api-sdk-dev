apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.4'
    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20160810'
    testCompile 'org.awaitility:awaitility:2.0.0'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.squareup.okhttp3:okhttp:3.4.+'
        classpath 'com.fasterxml.jackson.core:jackson-databind:2.8.+'
    }
}

def repo = new File(rootDir, "../repository")

apply plugin: "maven"

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${repo.absolutePath}"
            pom.version = '0.1.0'
            pom.groupId = 'pw.anothersky.movabletype.apisdk'
            pom.artifactId = 'mt-data-api-sdk-android'
        }
    }
}

android.libraryVariants.all { variant ->

    if (variant.name == "debug") {
        return
    }

    task("generateJavadoc", type: Javadoc) {
        title = "MT Data API SDK JavaDoc"
        description "Generates Javadoc for Movable Type Data API SDK for Android."
        source = variant.javaCompile.source
        ext.androidJar =
                "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        destinationDir = file("../../docs")
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        options.charSet("utf-8")
        options.locale("ja_JP")
        options.showFromPublic()
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
